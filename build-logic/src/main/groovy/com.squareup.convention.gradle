//file:noinspection UnnecessaryQualifiedReference
plugins {
  id 'org.jetbrains.kotlin.jvm'
}

group = providers.gradleProperty('GROUP').get()
version = providers.gradleProperty('VERSION').get()

def javaVersion = JavaVersion.VERSION_11
java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = javaVersion
  }
}

tasks.withType(GroovyCompile).configureEach {
  options.incremental = true
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

// Exclude Junit4. We really want to enforce Junit5 and unfortunately, Truth has a transitive dependency on 4.
configurations.all {
  exclude group: 'junit', module: 'junit'
  exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

dependencies {
  // Align all Kotlin components on the same version
  implementation(platform('org.jetbrains.kotlin:kotlin-bom'))

  // JUnit5 / Jupiter Platform stuff
  testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2') {
    because 'For running tests on the JUnit5 Jupiter platform'
  }
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
  testImplementation 'com.google.truth:truth:1.1.3'
}

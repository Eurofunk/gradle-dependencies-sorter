//file:noinspection UnnecessaryQualifiedReference
plugins {
  id 'com.squareup.base'
  id 'application'
  id 'com.github.johnrengelman.shadow'
}

def isSnapshot = version.toString().endsWith('SNAPSHOT')

publishing {
  repositories {
    def sonatypeUsername = username()
    def sonatypePassword = password()
    if (sonatypeUsername != null && sonatypePassword != null) {
      maven {
        name = 'sonatype'

        def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
        def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
        url = isSnapshot ? snapshotsRepoUrl : releasesRepoUrl

        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
      }
    }
  }
}

def username() {
  def name = 'sonatypeUsername'
  return (project.properties[name] ?: System.getenv(name))?.toString()
}

def password() {
  def name = 'sonatypePassword'
  return (project.properties[name] ?: System.getenv(name))?.toString()
}

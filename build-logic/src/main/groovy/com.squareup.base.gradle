//file:noinspection UnnecessaryQualifiedReference
plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'groovy'
  id 'maven-publish'
  id 'org.gradle.signing'
}

group = providers.gradleProperty('GROUP').get()
version = providers.gradleProperty('VERSION').get()

def versionCatalog = extensions.getByType(VersionCatalogsExtension).named('libs')
def javaVersion = versionCatalog.findVersion('java').orElseThrow().requiredVersion
java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(javaVersion))
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = javaVersion
  }
}

tasks.withType(GroovyCompile).configureEach {
  options.incremental = true
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

// Exclude Junit4. We really want to enforce Junit5 and unfortunately, Truth has a transitive dependency on 4.
configurations.all {
  exclude group: 'junit', module: 'junit'
  exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

dependencies {
  // Align all Kotlin components on the same version
  implementation(platform('org.jetbrains.kotlin:kotlin-bom'))

  // JUnit5 / Jupiter Platform stuff
  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine

  testImplementation libs.truth
}
